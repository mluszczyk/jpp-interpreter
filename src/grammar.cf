comment "--" ;

layout "let", "where", "of" ;
layout stop "in" ;
layout toplevel ;

token TypeIdent (upper (letter | digit | '_')*) ;
token ValueIdent (lower (letter | digit | '_')*) ;

Program. Program ::= [Decl] ;

ELet. Exp ::= "let" "{" [Decl] "}" "in" Exp ;
ECase. Exp ::= "case" Exp "of" "{" [CasePart] "}" ;
ELambda. Exp ::= "\\" ValueIdent "->" Exp ;
EIf. Exp ::= "if" Exp "then" Exp "else" Exp ;

EAnd. Exp1 ::= Exp1 "&&" Exp2 ;
EOr. Exp1 ::= Exp1 "||" Exp2 ;

ELT. Exp2 ::= Exp2 "<" Exp3 ;
ELTE. Exp2 ::= Exp2 "<=" Exp3 ;
EGT. Exp2 ::= Exp2 ">" Exp3 ;
EGTE. Exp2 ::= Exp2 ">=" Exp3 ;
EEq. Exp2 ::= Exp2 "==" Exp3 ;
ENEq. Exp2 ::= Exp2 "/=" Exp3 ;

EAdd.	Exp3	::=	Exp3	"+"	Exp4	;
ESub.	Exp3	::=	Exp3	"-"	Exp4	;
EMul.	Exp4	::=	Exp4	"*"	Exp5	;
EDiv.	Exp4	::=	Exp4	"`div`"	Exp5	;

EApp. Exp10 ::= Exp10 Exp11;

EInt.	Exp11	::=	Integer	;
EVarValue. Exp11 ::= ValueIdent ;
EVarType. Exp11 ::= TypeIdent ;
coercions	Exp	11	;

DValue. Decl ::= ValueIdent [ValueIdent] "=" Exp ;
DValueWhere. Decl ::= ValueIdent [ValueIdent] "=" Exp "where" "{" [Decl] "}" ;
DType. Decl ::= ValueIdent "::" TypeRef ;
DData. Decl ::= "data" TypeDecl "=" [Variant] ;

CaseP. CasePart ::= Pattern "->" Exp;
separator CasePart ";" ;

PVariant. Pattern ::= TypeIdent [Pattern1] ;
PValue. Pattern1 ::= ValueIdent ;
PAny. Pattern1 ::= "_" ;
coercions Pattern 1 ;
separator Pattern1 "" ;

TDecl. TypeDecl ::= TypeIdent [ValueIdent] ;
separator ValueIdent "" ;
-- e.g. `Blah a b c`

TRVariant. TypeRef ::= TypeIdent [TypeRef1] ;
TRFunc. TypeRef1 ::= TypeRef2 "->" TypeRef1 ;
TRValue. TypeRef2 ::= ValueIdent ;
TRSimpleVariant. TypeRef2 ::= TypeIdent ;
coercions TypeRef 2;
separator nonempty TypeRef1 "" ;

-- e.g. `Blah (Blah a b c) [Blah] ;

Var. Variant ::= TypeIdent [TypeRef1] ;
SimpleVar. Variant ::= TypeIdent ;

separator Ident "" ;
separator Variant "|" ;
separator Decl ";" ;
